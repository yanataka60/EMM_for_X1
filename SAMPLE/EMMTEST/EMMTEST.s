INPUTF		EQU	0003H	;１行入力
PRINT		EQU	000BH	;文字列のプリント
ACCPRT		EQU	0013H	;１文字出力
INKEYD		EQU	001BH	;１文字入力
CR2		EQU	04A3H	;行の先頭でないなら改行
CR1		EQU	04A7H	;改行
ACCDIS		EQU	04C8H	;１文字出力 コントロール・コード表示版
MONOP		EQU	0FE2H	;モニタコールドスタート
HOTSTA		EQU	1003H	;モニタホットスタート
ACHXPR		EQU	1207H	;Aレジスタの値を16進数で出力
TUPPER		EQU	1451H	;大文字に変換
IPLFCB		EQU	1480H	;CZ-8CB01
;FNAME		EQU	IPLFCB+1
;FSIZE		EQU	IPLFCB+18
;SADRS		EQU	IPLFCB+20
;EXEAD		EQU	IPLFCB+22


		ORG	14A0H

		LD	DE,TITLE			;'******** EMM RAM TEST MENU ********'表示
		CALL	PRINT
		CALL	CR1
MENU:		CALL	CR1
		LD	DE,EMENU			;'0)EMM0 1)EMM1 2)EMM2 3)EMM3 ?'表示
		CALL	PRINT
		LD	A,01H				;一文字入力,キー入力待ち
		CALL	INKEYD
		LD	(MSG1+3),A			;EMM0～EMM3の文字を変更
		LD	(MSG4+3),A

		CP	'0'				;EMM0
		JR	NZ,EMM1
		LD	HL,0D00H			;EMM I/Oアドレス 0D00H
		LD	(EMMADRS),HL
		JR	MENUS
EMM1:		CP	'1'				;EMM1
		JR	NZ,EMM2
		LD	HL,0D04H			;EMM I/Oアドレス 0D04H
		LD	(EMMADRS),HL
		JR	MENUS
EMM2:		CP	'2'				;EMM2
		JR	NZ,EMM3
		LD	HL,0D08H			;EMM I/Oアドレス 0D08H
		LD	(EMMADRS),HL
		JR	MENUS
EMM3:		CP	'3'				;EMM3
		JR	NZ,MENU
		LD	HL,0D0CH			;EMM I/Oアドレス 0D0CH
		LD	(EMMADRS),HL
		
MENUS:
		JR	LSTART				;EMMTEST

EXIT:		CALL	CR1
		LD	DE,ENDMSG			;'Run Again:[G 14A0],If not:[IPL RESET]'表示
		CALL	PRINT
		JP	HOTSTA

LSTART:
;********************************** EMMLOAD処理本体 ************************************
ST0:		CALL	CR1
ML1:
		LD	DE,MSG0				;' CHECK START!'表示
		CALL	PRINT

		XOR	A
		LD	(ADRSL),A
		LD	(ADRSM),A
		LD	(ADRSH),A

		CALL	CR1
LOP1:		
		LD	A,(ADRSL)
		AND	A
		JR	NZ,LOP2
		CALL	PRTADRS
		CALL	CR1

LOP2:
		CALL	EMMREAD
		LD	(WORK1),A
		LD	A,55H
		CALL	EMMTEST
		CP	55H
		JR	NZ,EMMERR
		LD	A,0AAH
		CALL	EMMTEST
		CP	0AAH
		JR	NZ,EMMERR
		LD	A,(ADRSL)
		CALL	EMMTEST
		LD	HL,ADRSL
		CP	(HL)
		JR	NZ,EMMERR
		LD	A,(WORK1)
		CALL	EMMTEST
		LD	HL,WORK1
		CP	(HL)
		JR	NZ,EMMERR
		LD	HL,(ADRSL)
		INC	HL
		LD	(ADRSL),HL
		LD	A,L
		OR	H
		JR	NZ,LOP1
		LD	A,(ADRSH)
		INC	A
		LD	(ADRSH),A
		CP	08H
		JR	NZ,LOP1
		
		LD	DE,MSG1				;'EMMx MEMORY TEST END'を表示
		CALL	PRINT
		JP	EXIT				;終了

EMMERR:
		CALL	CR1
		LD	DE,MSG4				;'EMMx CHECK ERROR!'を表示
		CALL	PRINT
		JP	EXIT				;終了

EMMTEST:
;		PUSH	AF
;		CALL	ACHXPR
;		POP	AF
		CALL	EMMWRITE
		CALL	EMMREAD
;		PUSH	AF
;		CALL	ACHXPR
;		POP	AF
		RET

EMMREAD:
		LD	BC,(EMMADRS)
		LD	A,(ADRSL)			;EMMアドレスセット
ML2:		OUT	(C),A
		LD	A,(ADRSM)
		INC	BC
		OUT	(C),A
		LD	A,(ADRSH)
		INC	BC
		OUT	(C),A
		INC	BC
		IN	A,(C)
		RET

EMMWRITE:
		PUSH	AF
		LD	BC,(EMMADRS)
		LD	A,(ADRSL)			;EMMアドレスセット
ML4:		OUT	(C),A
		LD	A,(ADRSM)
		INC	BC
		OUT	(C),A
		LD	A,(ADRSH)
		INC	BC
		OUT	(C),A
		POP	AF
		INC	BC
		OUT	(C),A
		RET

PRTADRS:
		LD	A,(ADRSH)
		CALL	ACHXPR
		LD	A,(ADRSM)
		CALL	ACHXPR
		LD	A,(ADRSL)
		CALL	ACHXPR
		RET
		
TITLE:
		DB	'******** EMM RAM TEST MENU ********'
		DB	00H

EMENU:		DB	'0)EMM0 1)EMM1 2)EMM2 3)EMM3 ?'
		DB	00H

ENDMSG:		DB	'Run Again:[G 14A0],If not:[IPL RESET]'
		DB	00H

EMMADRS:	DW	0D00H

MSG0:		DB	' RAM TEST START!'
		DB	00H

MSG1:
		DB	'EMM0 RAM TEST OK!'
		DB	00H

MSG4:
		DB	'EMM0 RAM ERROR!'
		DB	00H

ADRSL:		DB	00H
ADRSM:		DB	00H
ADRSH:		DB	00H
WORK1:		DB	00H

ENT6:
			END
